defaultTasks 'installApp'

evaluationDependsOn(':src:network')

apply plugin: 'application'
apply plugin: 'java'

applicationName = 'jxclient'
mainClassName = 'com.realtime.crossfire.jxclient.main.JXClient'
run.jvmArgs '-ea'

jar.baseName = 'jxclient'
jar.classifier = 'plain'

dependencies {
    compile 'com.googlecode.soundlibs:mp3spi:1.9.5.4'
    compile 'org.jetbrains:annotations:17.0.0'
    compile project(':src:model')
    compile project(':src:network')
    compile project(':src:protocol')
    compile project(':src:resources')
    compile project(':src:skins')
    compile project(':src:util')
    testCompile 'junit:junit:4.12'
    testCompile project(':src:network').sourceSets.test.output
}

task createJar(type: Jar, dependsOn: assemble) {
    description = 'Creates the jxclient.jar application.'
    baseName = 'jxclient'
    manifest.attributes 'Main-Class': mainClassName
    manifest.attributes 'SplashScreen-Image': 'resource/splashscreen.png'
    from sourceSets.main.output
    from {
        configurations.runtime.filter {
            it.name != 'annotations-0.0.jar'
        }.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
